* >Natural Source Header 000000
* :Mode S
* :CP IBM01140
* :LineIncrement 10
* <Natural Source Header
* Requires:
* 01 #MAX-PDA-OCC (I2)
* 01 #MAX-XARRAY-OCC (I2)
* 01 #TEMP-OCC(I2)
* 01 #STARTING-PDA-INDEX (I2)
* 01 #STARTING-ATTRIBUTE(I2) INIT <1>
* 01 #ENDING-ATTRIBUTE(I2)
* 01 #XARRAY-START-INDEX (I2)
* 01 #XARRAY-END-INDEX (I2)
* and +PDA* variables
*
* pass in:
*   level number &1&
*   field-format &2&
*   field-name &3&
/* Determine the items in the PDA for the level one being processed
#MAX-PDA-OCC := *OCCURRENCE(+PDA-VARIABLE-NAME)
IF &1& = 1 AND &2& NE " " THEN
/* we have a level 1 xarray
  EXAMINE FULL +XARRAY-NAME(*) FOR FULL &3& GIVING INDEX
    #STARTING-PDA-INDEX
  #XARRAY-START-INDEX  := #STARTING-PDA-INDEX
  #XARRAY-END-INDEX  := #STARTING-PDA-INDEX
  IF #STARTING-PDA-INDEX NE 0 THEN
    #STARTING-ATTRIBUTE := +XARRAY-ATTRIBUTE-NUMBER(#STARTING-PDA-INDEX)
    #ENDING-ATTRIBUTE := +XARRAY-ATTRIBUTE-NUMBER(#STARTING-PDA-INDEX)
  END-IF
ELSE
  EXAMINE FULL +PDA-VARIABLE-NAME(*) FOR FULL &3& GIVING INDEX
    #STARTING-PDA-INDEX
  #STARTING-ATTRIBUTE := +PDA-ATTRIBUTE-NUMBER(#STARTING-PDA-INDEX)
  #TEMP-OCC := #STARTING-PDA-INDEX + 1
  FOR #I = #TEMP-OCC TO #MAX-PDA-OCC
    IF +PDA-LEVEL-NUMBER(#I) = 1 THEN
      #TEMP-OCC := #I  - 1
      #ENDING-ATTRIBUTE := +PDA-ATTRIBUTE-NUMBER(#TEMP-OCC)
      ESCAPE BOTTOM
    END-IF
    IF #ENDING-ATTRIBUTE = 0 THEN
      #ENDING-ATTRIBUTE := #MAX-PDA-OCC
    END-IF
  END-FOR
  IF #ENDING-ATTRIBUTE = 0 THEN
    #ENDING-ATTRIBUTE := #STARTING-ATTRIBUTE
  END-IF
/* Determine the range of items in the xarray pda based on the
/* attribute numbers
  #MAX-XARRAY-OCC := *OCCURRENCE(+XARRAY-NAME)
  RESET #XARRAY-START-INDEX #XARRAY-END-INDEX
  FOR #J = 1 TO #MAX-XARRAY-OCC
    IF +XARRAY-ATTRIBUTE-NUMBER(#J) >= #STARTING-ATTRIBUTE THEN
      #XARRAY-START-INDEX := #J
      ESCAPE BOTTOM
    END-IF
  END-FOR
  FOR #J = #MAX-XARRAY-OCC TO 1 STEP -1
    IF +XARRAY-ATTRIBUTE-NUMBER(#J) <= #ENDING-ATTRIBUTE THEN
      #XARRAY-END-INDEX := #J
      ESCAPE BOTTOM
    END-IF
  END-FOR
  IF #XARRAY-START-INDEX = 0 AND #XARRAY-END-INDEX NE 0 THEN
    RESET #XARRAY-END-INDEX
  END-IF
