* >Natural Source Header 000000
* :Mode S
* :CP IBM01140
* :LineIncrement 10
* <Natural Source Header
* This code is used to assign values to +XARRAY-LDA-NAME
* +XARRAY-LDA-OCC-NAME, +XARRAY-PDA-OCC-NAME +XARRAY-PDA-NAME
* +XARRAY-BASE-INDEX and +XARRAY-FULL-INDEX
* The variables required for this copy code are:
* +PDA-LEVEL1-NAME
* +PDA-RANK
* +PDA-FIELD-FORMAT
* #SEP
* +PDA-VARIABLE-NAME
* 01 #PDA-THRU-INDEX (N10/3)
* 01 REDEFINE #PDA-THRU-INDEX
*   02 #PDA-INDEXES(3)
*     03 #PDA-XARRAY (L)
* #J (I2)
* #XARRAY-IND (I2)
* #OCC-TEXT (A20)
* #MAT-OCC-TEXT (A105)
* #DIM-OCC-DIFFERENCE (N1)
* +XARRAY-DIM (N1/*)
* +XARRAY-BASE-INDEX (A13/*)
* +XARRAY-FULL-INDEX (A107/*)
* +XARRAY-LDA-NAME
* +XARRAY-LDA-OCC-NAME
* +XARRAY-DIM-DESCRIPTION
COMPRESS +PDA-LEVEL1-NAME(#I) #SEP +PDA-VARIABLE-NAME(#I) INTO
  +XARRAY-LDA-NAME(#XARRAY-IND) LEAVING NO
+XARRAY-DIM(#XARRAY-IND) := +PDA-RANK(#I)
/* *occurrence cannot be one on group level fields so find next
/* elementary field
IF +PDA-FIELD-FORMAT(#I) NE " " THEN
  +XARRAY-LDA-OCC-NAME(#XARRAY-IND) := +PDA-VARIABLE-NAME(#I)
  COMPRESS +PDA-LEVEL1-NAME(#I) #SEP
    +XARRAY-LDA-OCC-NAME(#XARRAY-IND)  INTO
    +XARRAY-LDA-OCC-NAME(#XARRAY-IND) LEAVING NO
ELSE
  #START := #I + 1
  FOR #K = #START TO #MAX-PDA
    IF +PDA-FIELD-FORMAT(#K) NE " " THEN
        +XARRAY-LDA-OCC-NAME(#XARRAY-IND)
        := +PDA-VARIABLE-NAME(#K)
      ESCAPE BOTTOM
    END-IF
  END-FOR
  #SEP := '.'
  IF +PDA-LEVEL1-NAME(#I) = " " THEN
    COMPRESS +PDA-VARIABLE-NAME(#I) #SEP
      +XARRAY-LDA-OCC-NAME(#XARRAY-IND)  INTO
      +XARRAY-LDA-OCC-NAME(#XARRAY-IND) LEAVING NO
  ELSE
    COMPRESS +PDA-LEVEL1-NAME(#I) #SEP
      +XARRAY-LDA-OCC-NAME(#XARRAY-IND)  INTO
      +XARRAY-LDA-OCC-NAME(#XARRAY-IND) LEAVING NO
  END-IF
  RESET #SEP
END-IF
/* Note that the CAT variables will change names in CUOGGEN if categorization is
/* turned on and the user exits are changed
RESIZE ARRAY +XARRAY-PDA-NAME TO (1:#XARRAY-IND)
RESIZE ARRAY +XARRAY-PDA-OCC-NAME TO (1:#XARRAY-IND)
+XARRAY-PDA-OCC-NAME(*) := +XARRAY-LDA-OCC-NAME(*)
+XARRAY-PDA-NAME(*) := +XARRAY-LDA-NAME(*)
RESET #OCC-TEXT
/* Determine what actual dimension of the array are
/* including the dimensions that were defined at higher levels
PERFORM POPULATE-XARRAY-DIM-DESCRIPTION
/* Determine what xarray definitions are at this level
MOVE +PDA-THRU-INDEX(#I,*) TO #PDA-THRU-INDEX(*)
#XARRAY-AT-THIS-LEVEL := FALSE
RESET #DIM-TEMP
FOR #J = 1 TO 3
  IF #PDA-XARRAY(#J)
    #XARRAY-AT-THIS-LEVEL := TRUE
    ADD 1 TO #DIM-TEMP
  ELSE
    IF #PDA-THRU-INDEX(#J) NE 0 THEN
      ADD 1 TO #DIM-TEMP
    END-IF
  END-IF
END-FOR
/* fill in the array values
IF #XARRAY-AT-THIS-LEVEL
  #START-LEVEL-OCC := +XARRAY-DIM(#XARRAY-IND) - #DIM-TEMP
  /* reserve all the dimensions not defined at this level
  FOR #J = 1 TO #START-LEVEL-OCC
     COMPRESS  +XARRAY-BASE-INDEX(#XARRAY-IND) '*' INTO +XARRAY-BASE-INDEX(#XARRAY-IND)
       WITH DELIMITER ','
     COMPRESS  +XARRAY-FULL-INDEX(#XARRAY-IND) '*' INTO +XARRAY-FULL-INDEX(#XARRAY-IND)
         WITH DELIMITER ','
  END-FOR
  ADD 1 TO #START-LEVEL-OCC
  /* allocate dimensions defined at this level
  FOR #J = #START-LEVEL-OCC TO +XARRAY-DIM(#XARRAY-IND) /* find all dimensions
     IF +XARRAY-DIM-DESCRIPTION(#XARRAY-IND,#J) = " " THEN
       ESCAPE TOP /* we've processed all the dimensions
     END-IF
     IF +XARRAY-DIM-DESCRIPTION(#XARRAY-IND,#J) = 'A' THEN
       COMPRESS +XARRAY-BASE-INDEX(#XARRAY-IND) '1:1' INTO
        +XARRAY-BASE-INDEX(#XARRAY-IND) WITH DELIMITER ','
       COMPRESS  +XARRAY-FULL-INDEX(#XARRAY-IND) '1:#MATERIALIZED-VARS.#XARRAY-OCC'
        INTO +XARRAY-FULL-INDEX(#XARRAY-IND) WITH DELIMITER ','
     ELSE
       COMPRESS  +XARRAY-BASE-INDEX(#XARRAY-IND) '*' INTO +XARRAY-BASE-INDEX(#XARRAY-IND)
         WITH DELIMITER ','
       COMPRESS  +XARRAY-FULL-INDEX(#XARRAY-IND) '*' INTO +XARRAY-FULL-INDEX(#XARRAY-IND)
         WITH DELIMITER ','
     END-IF
  END-FOR
END-IF
/* detemine how many dimensions are xarrays
* EXAMINE +XARRAY-DIM-DESCRIPTION(#XARRAY-IND,*) FOR 'A' GIVING #DIM-TEMP
IF #DIM-TEMP > 1 THEN
  COMPRESS #DIM-TEMP '!' +XARRAY-FULL-INDEX(#XARRAY-IND) #MAT-OCC-TEXT INTO
    +XARRAY-FULL-INDEX(#XARRAY-IND) LEAVING NO
END-IF
*
COMPRESS '(' +XARRAY-BASE-INDEX(#XARRAY-IND) ')' INTO
  +XARRAY-BASE-INDEX(#XARRAY-IND) LEAVING NO
COMPRESS '(' +XARRAY-FULL-INDEX(#XARRAY-IND) ')' INTO
  +XARRAY-FULL-INDEX(#XARRAY-IND) LEAVING NO
END-IF
END-FOR
