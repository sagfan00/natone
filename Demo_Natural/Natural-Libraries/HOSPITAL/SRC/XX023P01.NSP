* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
* THIS PROGRAM LISTS THE PATIENTS BY PATIENT-ID
*
DEFINE DATA GLOBAL USING XX000G00
LOCAL USING XX021L01
LOCAL
01 #W-LOOP(P3)
01 #W-PAGE-NUMBER(P3)
01 #M-SELECTED(A1/1:15)
01 #C-SELECTED(C)
01 #M-YEAR(N2)
01 REDEFINE #M-YEAR
  02 #M-YEAR-ALPHA(A2)
*
01 #W-PAGE-KEY(N7/1:100)
01 #W-START-KEY(N7)
01 #M-NAME(A40/1:15)
01 #M-PATIENT-ID(N7/1:15)
01 #W-END-OF-DATA(L)
END-DEFINE
*
SET KEY ALL
*
MOVE 1 TO #W-PAGE-NUMBER
*
* SET UP THE FIRST READ KEY!
*
INPUT #M-YEAR
MOVE #M-YEAR TO #W-PAGE-KEY(1)
MULTIPLY #W-PAGE-KEY(1) BY 100000 GIVING #W-PAGE-KEY(1)
PERFORM READ-DATA
*
REPEAT
*
INPUT USING MAP "XX023M01"
RESET #G-MESSAGE
*
DECIDE ON FIRST VALUE OF *PF-KEY
*
VALUE "PF12", "PF24"
    PERFORM XXEXIT
VALUE "PF3", "PF15"
    ESCAPE ROUTINE
VALUE "PF8", "PF20"
    IF #W-END-OF-DATA
       MOVE "NO MORE DATA AVAILABLE" TO #G-MESSAGE
       ESCAPE TOP
    END-IF
    IF #W-PAGE-NUMBER = 100
       MOVE "SORRY, ONLY 100 PAGES AVAILABLE" TO #G-MESSAGE
       ESCAPE TOP
    END-IF
    ADD 1 TO #W-PAGE-NUMBER
    PERFORM READ-DATA
    ESCAPE TOP
VALUE "PF7", "PF19"
    IF #W-PAGE-NUMBER = 1
       MOVE "CANNOT PAGE BACK FROM FIRST PAGE" TO #G-MESSAGE
       ESCAPE TOP
    END-IF
    SUBTRACT 1 FROM #W-PAGE-NUMBER
    PERFORM READ-DATA
    MOVE FALSE TO #W-END-OF-DATA
    ESCAPE TOP
VALUE "ENTR"
    IGNORE
NONE VALUE
    MOVE "INVALID PF KEY PRESSED" TO #G-MESSAGE
END-DECIDE
*
END-REPEAT
*
DEFINE SUBROUTINE READ-DATA
*
RESET #W-LOOP
MOVE #W-PAGE-KEY(#W-PAGE-NUMBER) TO #W-START-KEY
READ PATIENT BY PATIENT-ID STARTING FROM #W-START-KEY
IF #V-NUMBER = 99999
   ESCAPE TOP
END-IF
*
IF #V-YY NOT = #M-YEAR-ALPHA
   ESCAPE BOTTOM
END-IF
*
ADD 1 TO #W-LOOP
*
IF #W-LOOP = 16
   MOVE PATIENT-ID TO #W-PAGE-KEY(#W-PAGE-NUMBER + 1)
   ESCAPE ROUTINE
END-IF
*
MOVE PATIENT-ID TO #M-PATIENT-ID(#W-LOOP)
COMPRESS FIRST-NAME SURNAME INTO #M-NAME(#W-LOOP)
*
END-READ
*
MOVE TRUE TO #W-END-OF-DATA
*
END-SUBROUTINE
*
END
