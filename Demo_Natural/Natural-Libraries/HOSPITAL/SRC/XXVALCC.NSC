* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
* A LOVELY PIECE OF COPYCODE WITH EXTRA DATE VALIDATION FOR
* THE ARRIVAL DATE!
*
*
DEFINE SUBROUTINE VALIDATE-ARRIVED
*
MOVE #V-VAL(1) TO #W-DD1
MOVE #V-VAL(2) TO #W-DD2
*
IF #W-DD < "01" OR #W-DD > "31" OR #W-DD NOT = MASK(NN)
   MOVE "INVALID DAY NUMBER SPOTTED - FORMAT IS DD-MONTH-YY!"
       TO #W-MESSAGE
   MOVE 8 TO #W-MARK
   PERFORM SET-MESSAGE
   ESCAPE ROUTINE
END-IF
*
IF #V-VAL(3) NOT = "-"
   MOVE "POSITION 3 MUST BE A '-' - FORMAT IS DD-MONTH-YY!"
       TO #W-MESSAGE
   MOVE 8 TO #W-MARK
   PERFORM SET-MESSAGE
   ESCAPE ROUTINE
END-IF
*
RESET #W-MM
FOR #W-LOOP = 4 TO 15
  IF #V-VAL(#W-LOOP) = "-"
     ESCAPE BOTTOM
  END-IF
  IF #W-LOOP > 4
     EXAMINE #V-VAL(#W-LOOP) AND TRANSLATE INTO LOWER
  ELSE
     EXAMINE #V-VAL(#W-LOOP) AND TRANSLATE INTO UPPER
  END-IF
  MOVE #V-VAL(#W-LOOP) TO #W-MM-PART(#W-LOOP - 3)
END-FOR
*
* SAVE THE CURRENT INDEX VALUE FOR LATER CHECKING!
*
MOVE #W-LOOP TO #W-SAVE-LOOP

*
MOVE FALSE TO #W-FOUND
FOR #W-LOOP = 1 TO 12
  IF #W-MM = #L-MONTHS(#W-LOOP)
     MOVE TRUE TO #W-FOUND
     ESCAPE BOTTOM
  END-IF
END-FOR
*
IF NOT #W-FOUND
   MOVE "INVALID MONTH DETECTED - FORMAT IS DD-MONTH-YY!"
      TO #W-MESSAGE
   MOVE 8 TO #W-MARK
   PERFORM SET-MESSAGE
   ESCAPE ROUTINE
END-IF
*
IF #W-DD-N > #L-DAYS(#W-LOOP) AND #W-LOOP NE 2
   MOVE "INVALID NUMBER OF DAYS DETECTED FOR THE MONTH" TO
       #W-MESSAGE
   MOVE 8 TO #W-MARK
   PERFORM SET-MESSAGE
   ESCAPE ROUTINE
END-IF
*
IF #V-VAL(#W-SAVE-LOOP) NOT = "-"
   MOVE "POSITION AFTER MONTH MUST BE A '-' - FORMAT IS DD-MONTH-YY!"
       TO #W-MESSAGE
   MOVE 8 TO #W-MARK
   PERFORM SET-MESSAGE
   ESCAPE ROUTINE
END-IF
*
MOVE #V-VAL(#W-SAVE-LOOP + 1) TO #W-YY1
MOVE #V-VAL(#W-SAVE-LOOP + 2) TO #W-YY2
IF #W-YY < "30" OR #W-YY > "99"
   MOVE "INVALID YEAR ENTERED - MUST BE GREATER THAN 30"
       TO #W-MESSAGE
   MOVE 8 TO #W-MARK
   PERFORM SET-MESSAGE
   ESCAPE ROUTINE
END-IF
*
IF #W-MM = #L-MONTHS(2)  /* THEN IT IS FEBURARY, CHECK LEAP YEAR!
   DIVIDE 4 INTO #W-YY-N REMAINDER #W-LOOP
   IF #W-LOOP = 0 /* CRUDE, BUT MUST BE A LEAP YEAR..FORGETTING 1900/
                  /* 2000 ARGUMENTS AS THIS IS A NON YR2000 PROGGIE!
      IF #W-DD-N > 29
         MOVE "INVALID NUMBER OF DAYS DETECTED - FEB IN A LEAP YEAR"
            TO #W-MESSAGE
         MOVE 8 TO #W-MARK
         PERFORM SET-MESSAGE
         ESCAPE ROUTINE
      END-IF
   ELSE
      IF #W-DD-N > 28
         MOVE "INVALID NUMBER OF DAYS DETECTED  -FEB NOT IN A LEAP YEAR"
           TO #W-MESSAGE
         MOVE 8 TO #W-MARK
         PERFORM SET-MESSAGE
         ESCAPE ROUTINE
      END-IF
   END-IF
END-IF
*
END-SUBROUTINE
