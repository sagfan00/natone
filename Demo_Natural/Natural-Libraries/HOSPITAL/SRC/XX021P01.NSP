* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
* THIS IS THE MAIN VALIDATION PROGRAM USED FOR INSERTS/DELETES/MODIFIES!
*
DEFINE DATA GLOBAL USING XX000G00
LOCAL USING XX021L01
LOCAL USING XX021L02
LOCAL USING XXMTHVAL
LOCAL
01 #C-GROUP(C/1:10)
01 REDEFINE #C-GROUP
  02 #C-PATIENT-ID(C)
  02 #C-FIRST-NAME(C)
  02 #C-SURNAME(C)
  02 #C-DOB(C)
  02 #C-RELEASED(C)
  02 #C-ADDRESS(C)
  02 #C-ARRIVED(C)
  02 #C-DUE-FOR-SURGERY(C)
01 #P-PATIENT-ID(N7)
*
01 #W-OK(L)
01 #W-DD(A2)
01 REDEFINE #W-DD
  02 #W-DD1(A1)
  02 #W-DD2(A1)
01 REDEFINE #W-DD
  02 #W-DD-N(N2)
01 #W-MM(A12)
01 REDEFINE #W-MM
02 #W-MM-PART(A1/1:12)
*
01 #W-LOOP(P3)
01 #W-SAVE-LOOP(P3)
01 #W-FOUND(L)
*
01 #W-MESSAGE(A70)
*
01 #W-MARK(P3)
*
01 #L-TEMP-RELEASED(A6)
01 REDEFINE #L-TEMP-RELEASED
  02 #L-TEMP-RELEASED-NUMERIC(N6)
*
01 #W-CONFIRM-UPDATE(A1)
*
01 #L-TEMP-DATE(N8)
01 REDEFINE #L-TEMP-DATE
  02 FILLER 2X
  02 #L-TEMP-DATE-N6(N6)
*
01 #W-MAP-MARK(P3)
*
01 #W-YY(A2)
01 REDEFINE #W-YY
  02 #W-YY1(A1)
  02 #W-YY2(A1)
01 REDEFINE #W-YY
  02 #W-YY-N(N2)
01 #W-DATE-D(D)
*
01 #M-MAP-HEADING(A40)
01 #M-UNDERLINE(A40)
END-DEFINE
*
SET KEY ALL
MOVE *DATN TO #L-TEMP-DATE
DECIDE ON FIRST VALUE OF #G-SELECTED-OPTION
VALUE "A"
  MOVE *DATX TO PATIENT.RELEASED
  CALLNAT "XXGETID" PATIENT.PATIENT-ID #L-TEMP-DATE-N6
  MOVE "              ADD A PATIENT" TO #M-MAP-HEADING
  MOVE "              *************" TO #M-UNDERLINE
  MOVE (AD=D) TO #C-GROUP(*)
  MOVE (AD=P) TO #C-PATIENT-ID
VALUE "D"
  PERFORM GET-RECORD
  MOVE (AD=P) TO #C-GROUP(*)
  MOVE "            DELETE A PATIENT" TO #M-MAP-HEADING
  MOVE "            ****************" TO #M-UNDERLINE
VALUE "M"
  PERFORM GET-RECORD
  MOVE "            MODIFY A PATIENT" TO #M-MAP-HEADING
  MOVE "            ****************" TO #M-UNDERLINE
  MOVE (AD=D) TO #C-GROUP(*)
  MOVE (AD=P) TO #C-PATIENT-ID
NONE VALUE
  WRITE "INVALID VALUE FOUND IN #G-SELECTED-OPTION!"
  STOP
END-DECIDE
*
REPEAT
*
INPUT MARK #W-MAP-MARK USING MAP "XX021M01"
*
MOVE (AD=D) TO #C-GROUP(*)
MOVE (AD=P) TO #C-PATIENT-ID
RESET #G-MESSAGE
*
* NEED TO CONVERT THE D FORMAT DATE INTO A SUITABLE NUMBER TO COMPARE
* AGAINST THE SURGERY DATE!
*
  MOVE EDITED PATIENT.RELEASED(EM=YYMMDD) TO #L-TEMP-RELEASED
*
DECIDE ON FIRST VALUE OF *PF-KEY
VALUE "PF12", "PF24"
   PERFORM XXEXIT
VALUE "PF3", "PF15"
   IF #G-SELECTED-OPTION NOT = "A"
      ESCAPE ROUTINE
   ELSE
     FETCH "XX002P01"
   END-IF
VALUE "ENTR"
   IF #G-SELECTED-OPTION NOT = "D"
     PERFORM VALIDATION
     IF #W-CONFIRM-UPDATE = "Y"
        ESCAPE ROUTINE
     END-IF
   ELSE
     SET CONTROL "WL70C5B5/5F"
     MOVE "N" TO #W-CONFIRM-UPDATE
     INPUT / "DO YOU REALLY WANT TO DELETE THIS PATIENT? - (Y/N)" #W-CONFIRM-UPDATE(AD=IMT)
     SET CONTROL "WB"
     IF #W-CONFIRM-UPDATE = "Y"
        FIND PATIENT WITH PATIENT-ID = #P-PATIENT-ID
        DELETE
        END TRANSACTION
        END-FIND
        ESCAPE ROUTINE
     END-IF
   END-IF
NONE VALUE
   MOVE "INVALID PF KEY PRESSED" TO #G-MESSAGE
END-DECIDE
*
END-REPEAT
*
* VALIDATION SUBROUTINE
*
DEFINE SUBROUTINE VALIDATION
*
MOVE TRUE TO #W-OK
MOVE "N" TO #W-CONFIRM-UPDATE
*
DECIDE FOR EVERY CONDITION
*
WHEN PATIENT.FIRST-NAME = ' '
   MOVE (AD=I) TO #C-FIRST-NAME
   MOVE "FIRST NAME MUST BE ENTERED" TO #W-MESSAGE
   MOVE 1 TO #W-MARK
   PERFORM SET-MESSAGE
WHEN PATIENT.SURNAME = ' '
   MOVE (AD=I) TO #C-SURNAME
   MOVE "LAST NAME MUST BE ENTERED" TO #W-MESSAGE
   MOVE 2 TO #W-MARK
   PERFORM SET-MESSAGE
WHEN PATIENT.DOB > 980102 OR #V-DOB NOT = MASK(MMDDYY)
   MOVE (AD=I) TO #C-DOB
   MOVE "MUST ENTERED DATE OF BIRTH - FORMAT MM-DD-YY" TO #W-MESSAGE
   MOVE 3 TO #W-MARK
   PERFORM SET-MESSAGE
WHEN NOT ( PATIENT.ADDRESS(*) NOT = ' ' )
   MOVE (AD=I) TO #C-ADDRESS
   MOVE "PLEASE ENTER AN ADDRESS" TO #W-MESSAGE
   MOVE 4 TO #W-MARK
   PERFORM SET-MESSAGE
*
* ATTEMPTED TO VALIDATE THE ARRIVAL DATE!
* FORMAT IS DD-NAMEOFMONTH-YY
*
WHEN PATIENT.ARRIVED = " "
    MOVE (AD=I) TO #C-ARRIVED
    MOVE "PLEASE ENTER ARRIVED DATE - FORMAT E.G. 01-JANUARY-96"
        TO #W-MESSAGE
    MOVE 8 TO #W-MARK
    PERFORM SET-MESSAGE
WHEN PATIENT.ARRIVED NOT = " "
    PERFORM VALIDATE-ARRIVED
WHEN PATIENT.DUE-FOR-SURGERY NOT = MASK(YYMMDD)
   MOVE (AD=I) TO #C-DUE-FOR-SURGERY
   MOVE 9 TO #W-MARK
   MOVE "INVALID DUE FOR SURGERY FORMAT - MUST BE YY-MM-DD" TO #W-MESSAGE
   PERFORM SET-MESSAGE
WHEN #L-TEMP-RELEASED-NUMERIC < #V-DUE-FOR-SURGERY-NUMERIC
   MOVE (AD=I) TO #C-RELEASED
   MOVE 10 TO #W-MARK
   MOVE "IMPOSSIBLE TO RELEASE BEFORE SURGERY!" TO #W-MESSAGE
   PERFORM SET-MESSAGE
*
* THE RELEASED FIELD IS DATE FORMAT AND EDIT MASK ON MAP IS CCYYMMDD, THEREFORE IT SHOULD
* VALIDATE ITSELF!
*
WHEN #W-OK
   CALLNAT "XXCONUPD" #W-CONFIRM-UPDATE #G-SELECTED-OPTION
   IF #W-CONFIRM-UPDATE = 'Y'
      IF #G-SELECTED-OPTION = "A"
        STORE PATIENT
        END TRANSACTION
        FETCH "XX002P01"
      ELSE
        /* MUST BE MODIFY
      FIND PATIENT-UPDATE WITH PATIENT-ID = #P-PATIENT-ID
         MOVE BY NAME PATIENT TO PATIENT-UPDATE
         UPDATE
         END TRANSACTION
         ESCAPE ROUTINE
       END-FIND
     END-IF
   END-IF
WHEN NONE
   MOVE "HOW DID WE GET HERE THEN MATEY!" TO #W-MESSAGE
   PERFORM SET-MESSAGE
END-DECIDE
*
END-SUBROUTINE
*
INCLUDE XXVALCC
*
DEFINE SUBROUTINE SET-MESSAGE
*
IF #G-MESSAGE = " "
   MOVE #W-MESSAGE TO #G-MESSAGE
   MOVE #W-MARK TO #W-MAP-MARK
   MOVE FALSE TO #W-OK
END-IF
*
END-SUBROUTINE
*
DEFINE SUBROUTINE GET-RECORD
*
  INPUT #P-PATIENT-ID
  FIND PATIENT WITH PATIENT-ID = #P-PATIENT-ID
  END-FIND
*
END-SUBROUTINE
*
END
