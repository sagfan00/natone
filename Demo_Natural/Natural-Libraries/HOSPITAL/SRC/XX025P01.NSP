* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
* THIS PROGRAM LISTS THE PATIENTS BY NAME
*
DEFINE DATA GLOBAL USING XX000G00
LOCAL USING XX021L01
LOCAL
01 #W-LOOP(P3)
01 #W-PAGE-NUMBER(P3)
01 #M-SELECTED(A1/1:15)
01 #C-SELECTED(C)
01 #M-YEAR(N2)
*
01 #W-PAGE-KEY(A27/1:100)
01 #W-START-KEY(A27)
01 #M-NAME(A40/1:15)
01 #M-PATIENT-ID(N7/1:15)
01 #W-END-OF-DATA(L)
01 #W-TEMP-KEY(A27)
01 REDEFINE #W-TEMP-KEY
  02 #W-TEMP-SURNAME(A20)
  02 #W-TEMP-PATIENT-ID(N7)
*
END-DEFINE
*
SET KEY ALL
*
MOVE 1 TO #W-PAGE-NUMBER
*
* SET UP THE FIRST READ KEY!
*
PERFORM READ-DATA
*
REPEAT
*
INPUT USING MAP "XX025M01"
RESET #G-MESSAGE
*
DECIDE ON FIRST VALUE OF *PF-KEY
*
VALUE "PF12", "PF24"
    PERFORM XXEXIT
VALUE "PF3", "PF15"
    ESCAPE ROUTINE
VALUE "PF8", "PF20"
    IF #W-END-OF-DATA
       MOVE "NO MORE DATA AVAIALBLE" TO #G-MESSAGE
       ESCAPE TOP
    END-IF
    IF #W-PAGE-NUMBER = 100
       MOVE "SORRY, ONLY 100 PAGES AVAILABLE" TO #G-MESSAGE
       ESCAPE TOP
    END-IF
    ADD 1 TO #W-PAGE-NUMBER
    PERFORM READ-DATA
    ESCAPE TOP
VALUE "PF7", "PF19"
    SUBTRACT 1 FROM #W-PAGE-NUMBER
    PERFORM READ-DATA
    MOVE FALSE TO #W-END-OF-DATA
    ESCAPE TOP
VALUE "ENTR"
    PERFORM PROCESS-SELECTED
NONE VALUE
    MOVE "INVALID PF KEY PRESSED" TO #G-MESSAGE
END-DECIDE
*
END-REPEAT
*
DEFINE SUBROUTINE READ-DATA
*
RESET #M-PATIENT-ID(*) #M-NAME(*) #M-SELECTED(*)
RESET #W-LOOP
MOVE #W-PAGE-KEY(#W-PAGE-NUMBER) TO #W-START-KEY
READ PATIENT BY S2-SURNAME-PATIENT-ID
*
IF #V-NUMBER = 99999
   ESCAPE TOP
END-IF
ADD 1 TO #W-LOOP
*
IF #W-LOOP = 16
   MOVE PATIENT-ID TO #W-TEMP-PATIENT-ID
   MOVE SURNAME TO #W-TEMP-SURNAME
   MOVE #W-TEMP-KEY TO #W-PAGE-KEY(#W-PAGE-NUMBER + 1)
   ESCAPE ROUTINE
END-IF
*
MOVE PATIENT-ID TO #M-PATIENT-ID(#W-LOOP)
COMPRESS FIRST-NAME SURNAME INTO #M-NAME(#W-LOOP)
*
END-READ
*
MOVE TRUE TO #W-END-OF-DATA
*
END-SUBROUTINE
*
DEFINE SUBROUTINE PROCESS-SELECTED
*
FOR #W-LOOP = 1 TO 15
    DECIDE FOR FIRST CONDITION
    WHEN #M-SELECTED(#W-LOOP) = " "
         ESCAPE TOP
    WHEN #M-SELECTED(#W-LOOP) NOT = " "  AND
         #M-PATIENT-ID(#W-LOOP) = 0
         MOVE "YOU SELECTED A BLANK LINE - TRY AGAIN!" TO #G-MESSAGE
         ESCAPE ROUTINE
    WHEN #M-SELECTED(#W-LOOP) = "D" OR = "M"
         MOVE #M-SELECTED(#W-LOOP) TO #G-SELECTED-OPTION
         FETCH RETURN "XX021P01" #M-PATIENT-ID(#W-LOOP)
         MOVE " " TO #M-SELECTED(#W-LOOP)
    WHEN NONE
         MOVE "INVALID CHARACTER FOUND IN SELECTION ARRAY" TO #G-MESSAGE
         ESCAPE ROUTINE
    END-DECIDE
END-FOR
PERFORM READ-DATA
END-SUBROUTINE
END
